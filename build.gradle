plugins {
    id 'war'
    id "com.diffplug.spotless" version "7.2.1"
}
description = 'JLab weather app'
group = 'org.jlab'
version = new File("${projectDir}/VERSION").text.trim()
ext.version = project.version
ext.releaseDate = new Date().format('MMM dd yyyy')
tasks.withType(JavaCompile).configureEach {
    options.release = 17
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}
repositories {
    mavenCentral()
}
dependencies {
    // Wildfly 37.0.1.Final provides:
    providedCompile 'jakarta.platform:jakarta.jakartaee-api:10.0.0'

    // Smoothness setup should install these into Wildfly
    providedCompile 'org.tuckey:urlrewritefilter:5.1.3'
}
war {
    archiveFileName = 'weather.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
    }
}
spotless {
    java {
        googleJavaFormat()
    }
}